#!/usr/bin/perl -w

=head1 NAME

marcdump - MARC record dump utility

=head1 SYNOPSIS

B<marcdump> [options] file(s)

=back

=cut

use warnings;
use strict;
use integer;

use lib qw( /home/alester/marc-record/lib );
use MARC::Record;
use Getopt::Long;

my $opt_print = 1;
my $opt_quiet = 0;
my $opt_stats = 1;
my @opt_field = ();
my $opt_help = 0;

my $rc =
    GetOptions(
	"print!"    => \$opt_print,
	"quiet"	    => \$opt_quiet,
	"stats!"    => \$opt_stats,
	"field=s"   => \@opt_field,
	"help"	    => \$opt_help,
	);

if ( not $rc || $opt_help || (@ARGV == 0) ) {
    print <DATA>;
    exit 1;
}
	
my %counts;
my %errors;
my @files = @ARGV;
for my $filename ( @files ) {
	$counts{$filename} = 0;
	$errors{$filename} = 0;

	warn "$filename\n" unless $opt_quiet;
	open( my $fh, "<", $filename ) or die "Couldn't open $filename: $!\n";

	while ( !eof($fh) ) {
		++$counts{$filename};
		warn "$counts{$filename} records\n" if ( !$opt_quiet && ($counts{$filename} % 1000 == 0) );
		my $marc = MARC::Record::next_from_file( $fh ) or die $MARC::Record::ERROR;

		if ( @opt_field ) {
			$marc = $marc->clone( @opt_field );
		}

		print $marc->as_formatted, "\n\n" if $opt_print;
		if ( $marc->warnings() ) {
			++$errors{$filename};
			print join( "\n", $marc->warnings(), "" );
		}
	} # while
	close $fh;
} # for 

if ( $opt_stats ) {
	print " Recs  Errs Filename\n";
	print "----- ----- --------\n";
	for my $key ( sort keys %counts ) {
		printf( "%5d %5d %s\n", $counts{$key}, $errors{$key}, $key );
	} # for
} # if stats

__END__
Usage: marcdump [options] file(s)

Options:
    --help	    Print this summary
    --[no]print	    Print a MicroLIF-style dump of each record
    --field=spec    Specify a field, and optional subfield, to be
		    included in the output.  There may be multiples.
		    Examples:
			--field=245
			--field=020a
			--field=6XX
    --[no]quiet	    Print status messages
    --[no]stats	    Print a statistical summary by file at the end
